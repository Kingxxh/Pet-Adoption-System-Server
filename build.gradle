plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.3' // 用于生成 Protobuf 和 gRPC 的 Java 类
    id 'com.github.johnrengelman.shadow' version '8.1.1' // Shadow 插件
}

group 'com.petadoption'
version '1.0-SNAPSHOT'

sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    // gRPC 相关依赖
    implementation 'io.grpc:grpc-netty-shaded:1.56.0'
    implementation 'io.grpc:grpc-protobuf:1.56.0'
    implementation 'io.grpc:grpc-stub:1.56.0'

    // Protocol Buffers 依赖
    implementation 'com.google.protobuf:protobuf-java:3.21.12'

    // javax.annotation 依赖
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // 测试依赖
    testImplementation 'junit:junit:4.13.2'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.12"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.56.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

shadowJar {
    archiveBaseName.set('grpc-pet-adoption-server')
    archiveClassifier.set('')
    archiveVersion.set('1.0-SNAPSHOT')

    manifest {
        attributes 'Main-Class': 'com.petadoption.server.PetAdoptionServer'
    }
}

